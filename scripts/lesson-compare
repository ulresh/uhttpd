#!/usr/bin/perl -w
# Автор: Юрий Решетников <reshu@mail.ru>

use strict;
use warnings;
use Reshu::Utils;

my(%tags, %steps, %lessons);
my $last = '';
xopen my($tag), '-|', 'git tag';
while(defined(my $line = <$tag>)) {
    chomp $line;
    if($line =~ /\Ac\d\d\d[a-z]?\z/) {
	++$tags{$line};
	++$steps{substr($line, 0, 4)};
	$last = $line if $line gt $last;
    }
    elsif($line =~ /\Alesson\d\d\.\d[a-z]?\z/) {
	++$lessons{substr($line, 6, 2)}{substr($line, 9)};
    }
}
xclose $tag;

my $lesson_number;
if($last) {
    $lesson_number = substr $last, 1, 2;
    unless($lessons{$lesson_number}{substr($last, 3, 1) + 1}) {
	$lesson_number = sprintf '%02d', $lesson_number + 1;
    }
}
else {
    $lesson_number = '01';
}

my($errors,$code,$prev_lesson_step);
xopen my($lesson), '<', "lesson/$lesson_number.md";
while(defined(my $line = <$lesson>)) {
    while($line =~ /\[(.*?)\]\(.*?\),\s*\[посмотреть\s+отличия\]\(\/..\/..\/compare\/(c\d\d\d[a-z]?)..(c\d\d\d[a-z])\)/g) {
	my($file,$t1,$t2) = ($1,$2,$3);
	# warn eval dw qw($file $t1 $t2);
	my $s1 = substr $t1, 0, 4;
	my $s2 = substr $t2, 0, 4;
	my $c1 = substr $t1, 4;
	my $c2 = substr $t2, 4;
	my $next = $c1; if($next) { ++$next; } else { $next = 'a'; }
	if($s1 ne $s2 || $c2 ne $next) {
	    ++$errors;
	    warn 'ошибка тегов ', eval dw qw($file $t1 $t2);
	}
	elsif($tags{$s1} || $tags{$t1} || $tags{$t2}) {
	    if($tags{$s1} && $tags{$t1} && $tags{$t2}) { next; }
	    else {
		++$errors;
		warn 'ошибка тегов ', eval dw qw($file $t1 $t2);
	    }
	}
	else {
	    my $lesson_step = substr($s1, 1, 2).'.'.substr($s1, 3);
	    if($prev_lesson_step && $prev_lesson_step ne $lesson_step) {
		$code .= "git merge lesson$prev_lesson_step -m \"Merge tag lesson$prev_lesson_step into compare\"\n";
	    }
	    $code .= "git tag -m _ -a $t1\n" unless $c1;
	    $code .= "git checkout lesson$lesson_step -- $file && git commit -m _ && git tag -m _ -a $t2\n";
	    $prev_lesson_step = $lesson_step;
	}
    }
}
xclose $lesson;

if(!$errors && $code) {
    print $code;
    if($prev_lesson_step) {
	print "git merge lesson$prev_lesson_step -m \"Merge tag lesson$prev_lesson_step into compare\"\n";
    }
    print "git push\ngit push origin --tags\n";
}

# Local Variables:
# mode: perl
# coding: utf-8-unix
# tab-width: 8
# End:
